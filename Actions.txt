*** Settings ***
Library           AppiumLibrary
Library           ./lib.py
Resource          ./ElementXPaths.txt
Resource          ./Constant.txt

*** Keywords ***
List More
    Click Element    ${LANDING_PAGE.TAIL_BUTTON}
    Wait Until Page Contains Element    ${HOME.MORE_BUTTON}    timeout=${TIMEOUT}
    Click Element    ${HOME.MORE_BUTTON}
    Wait Until Page Contains Element    ${MORE.ALBUM_LIST}    timeout=${TIMEOUT}

List Tracks
    Click Element    ${MORE.FIRST_ALBUM}
    Wait Until Page Contains Element    ${ALBUM_LIST.FIRST_TRACK}    timeout=${TIMEOUT}

Play Track
    Click Element    ${ALBUM_LIST.FIRST_TRACK}
    Comment    when the app run in another device
    #Wait Until Page Contains Element    xpath=//*[@resource-id="android:id/alertTitle"]
    #Click Element    xpath=//*[@resource-id="android:id/button1"]
    Wait Until Page Contains Element    ${NAVIGATION_BAR.BUTTON}    timeout=${TIMEOUT}

Scroll To Screen Button
    ${size}=    Get Element Size    ${MORE.ALBUM_LIST}
    Swipe    0    ${size['height']}    0    1

Count Albums
    ${texts}=    Get Empty List
    ${old}=    Set Variable    @{ZERO_LIST}
    : FOR    ${i}    IN RANGE    ${LOOP_TIMES}
    \    ${elements}=    Get Elements    ${MORE.ALBUM_TEXTS}
    \    ${texts}=    Get Text Set    ${elements}    ${texts}
    \    Scroll To Screen Button
    \    Run Keyword If    ${old} == ${texts}    Exit For Loop
    \    ${old}=    Set Variable    ${texts}
    ${length}=    Get Length    ${texts}
    [Return]    ${length}
